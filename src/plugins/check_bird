#!/usr/bin/perl -w

use strict;
use warnings;

use FindBin;
use lib "${FindBin::Bin}/../lib";

use Monitoring::Plugin;
use birdctl;

my $mp = Monitoring::Plugin->new(
  plugin => "check_bird", shortname => "BIRD", version => "0.1",
  usage => "Usage: %s --instance <instance> [ -r <table> -z -s <socket> ]",
);
$mp->add_arg(
  spec => "instance|i=s",
  help => "The name of the routing instance (like bgp session) to monitor.",
  required => 1,
);
$mp->add_arg(
  spec => "table|r=s",
  help => "The table to search for routes.",
  default => "master",
);
$mp->add_arg(
  spec => "zero|z",
  help => "Whether zero routes is an error.",
);
$mp->add_arg(
  spec => "socket|s=s",
  help => "The location of the BIRD control socket.",
  default => "/var/run/bird/bird.ctl",
);
$mp->getopts;

# Handle timeouts (also triggers on invalid command)
$SIG{ALRM} = sub { $mp->nagios_exit(CRITICAL, "Timeout (possibly invalid command)") };
alarm $mp->opts->timeout;

eval q{
  my $bird = new birdctl(socket => $mp->opts->socket, restrict => 1);

  # Get instance information
  my @status;
  foreach ($bird->long_cmd("show protocols " . $mp->opts->instance)) {
    # Find the first 1002 line.
    /^1002-/ and @status = split(/\s+/, substr($_, 5)) and last;
    # Fall through: no information found, print closing line.
    /^\d{4} / and $mp->nagios_exit(CRITICAL, $_);
  }

  # Check status
  if ($status[3] ne "up") {
    if ($status[5]) {
      $mp->nagios_exit(CRITICAL, "Protocol $status[0] is $status[3] - info: $status[5]");
    }
    else {
      $mp->nagios_exit(CRITICAL, "Protocol $status[0] is $status[3] - info: Protocol / Instance Down");
    }
  }

  # Inspect routes imported from this protocol
  $_ = $bird->cmd("show route table " . $mp->opts->table . " protocol " . $mp->opts->instance . " count");
  /^0014 (\d+) of \d+ routes for \d+ networks$/ or $mp->nagios_exit(CRITICAL, $_);

  # Final status
  $mp->nagios_exit(
    $mp->opts->zero && $1 eq "0" ? CRITICAL : OK,
    "Protocol / Instance $status[0] is $status[3] - $1 routes imported."
  );
};
if ($@) { $mp->nagios_exit(CRITICAL, $@); }
